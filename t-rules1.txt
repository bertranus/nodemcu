// 3,1 arg#status включено реле (свет) 1 или нет 0
// 3,2 arg#time 180, 720 - время работы после отключения света
// 3,3 arg#isLong - флаг времени включенного реле 0 - сброс, 1 - включен таймер3, 2 - свет горел долго
// 3,4 arg#lux - светимость
// Timer1 - таймер выключения реле после отключения света
// Timer2 - таймер максимального времени для mqtt
// Timer3 - таймер времени включения света (долго или нет)
// mqtt 100 - выключен, 101 включен
// реле GPIO,14 - 1 выкл, 0 вкл

On System#Boot do    //When the ESP boots, do
   TaskValueSet 3,2,180
   TaskValueSet 3,4,20
endon

// включение реле по включению света
on Lux#Value>[arg#lux] do

    if [arg#status]=0
        GPIO,14,0 // включить реле
        timerSet,1,0 // отключить таймер1
        TaskValueSet 3,1,1 // выставить флаг включения реле
        Publish nodemcu-t/light, on
    endif

    if [arg#isLong]=0
        TaskValueSet 3,3,1 // выставить флаг запуска реле замера времени наличия света
        TimerSet,3,[arg#time]
    endif

Endon

// выставить флаг, что свет горел долго
on Rules#Timer=3 do
    TaskValueSet 3,3,2
Endon

// свет выключился
on Lux#Value<[arg#lux] do
    if [arg#status]=1
        if [arg#isLong]=2
            Let,1,[arg#time]*4
            timerSet,1,[VAR#1]
        else
            timerSet,1,[arg#time]
        endif
        TaskValueSet 3,3,0 // сброс arg#isLong
        TaskValueSet 3,1,0 // сброс arg#status
        Publish nodemcu-t/light, off
    endif
Endon

// продолжение во 2-м сете
